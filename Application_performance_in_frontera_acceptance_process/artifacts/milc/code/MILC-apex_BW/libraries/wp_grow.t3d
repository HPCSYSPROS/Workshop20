; wp_grow.t3d: T3D assembler version of wp_grow.c 
        .ident wp_grow$c
        .psect  wp_grow@code,code
#include <mpp/asdef.h>
        CRI_REGISTER_NAMES      ; Use CRI defs for register names
         XUP <- 0
         YUP <- 1
         ZUP <- 2
         TUP <- 3
         TDOWN <- 4
         ZDOWN <- 5
         YDOWN <- 6
         XDOWN <- 7
         PLUS <- +1
         MINUS <- -1

; register names 
	 src <- a0	; address of source wilson_vector 
	 dest <- a1	; address of dest. half_wilson_vector 
	 dir <- a2	; direction 
	 sign <- a3	; sign: PLUS = +1, MINUS = -1 
	 color <- t1 	; loop over colors 
	 scratch <- t2	;scratch integer

wp_grow::
	bis zero,zero,color	; initialize color counter 
	; if (sign==-1) dir <- OPPDIR(dir) = 7-dir 
	bge	sign,SKIP1
	bis	zero,7,scratch
	subq	scratch,dir,dir
SKIP1:
	; switch(dir) 
	cmpeq	dir,0,scratch
	bne scratch,XUP_S
	cmpeq	dir,1,scratch
	bne scratch,YUP_S
	cmpeq	dir,2,scratch
	bne scratch,ZUP_S
	cmpeq	dir,3,scratch
	bne scratch,TUP_S
	cmpeq	dir,4,scratch
	bne scratch,TDOWN_S
	cmpeq	dir,5,scratch
	bne scratch,ZDOWN_S
	cmpeq	dir,6,scratch
	bne scratch,YDOWN_S
	cmpeq	dir,7,scratch
	bne scratch,XDOWN_S

XUP_S:
XUP_L:
	lds	ft0,0(src)	; src->h[0].c[i].real 
	lds	ft1,4(src)	; src->h[0].c[i].imag 
	lds	ft2,24(src)	; src->h[1].c[i].real 
	lds	ft3,28(src)	; src->h[1].c[i].imag 

	sts	ft0,0(dest)
	sts	ft1,4(dest)
	cpysn	ft0,ft0,ft6
	cpysn	ft2,ft2,ft5
	sts	ft2,24(dest)
	sts	ft3,28(dest)

	sts	ft3,48(dest)	;  d[1] * -i 
	sts	ft5,52(dest)
	sts	ft1,72(dest)	;  d[0] * -i 
	sts	ft6,76(dest)

	addq color,1,color
	addq src,8,src		; next color 
	addq dest,8,dest
	cmplt color,3,scratch
	bne scratch,XUP_L
	ret     zero,(ra)
YUP_S:
YUP_L:
	lds	ft0,0(src)	; src->h[0].c[i].real 
	lds	ft1,4(src)	; src->h[0].c[i].imag 
	lds	ft2,24(src)	; src->h[1].c[i].real 
	lds	ft3,28(src)	; src->h[1].c[i].imag 

	cpysn	ft0,ft0,ft5
	cpysn	ft1,ft1,ft6
	sts	ft0,0(dest)
	sts	ft1,4(dest)
	sts	ft2,24(dest)
	sts	ft3,28(dest)

	sts	ft2,48(dest)	;  d[1] * +1 
	sts	ft3,52(dest)
	sts	ft5,72(dest)	;  d[0] * -1 
	sts	ft6,76(dest)

	addq color,1,color
	addq src,8,src		; next color 
	addq dest,8,dest
	cmplt color,3,scratch
	bne scratch,YUP_L
	ret     zero,(ra)
ZUP_S:
ZUP_L:
	lds	ft0,0(src)	; src->h[0].c[i].real 
	lds	ft1,4(src)	; src->h[0].c[i].imag 
	lds	ft2,24(src)	; src->h[1].c[i].real 
	lds	ft3,28(src)	; src->h[1].c[i].imag 

	cpysn	ft0,ft0,ft5
	sts	ft0,0(dest)
	sts	ft1,4(dest)
	sts	ft2,24(dest)
	sts	ft3,28(dest)
	cpysn	ft3,ft3,ft6

	sts	ft1,48(dest)	;  d[0] * -i 
	sts	ft5,52(dest)
	sts	ft6,72(dest)	;  d[1] * +i 
	sts	ft2,76(dest)

	addq color,1,color
	addq src,8,src		; next color 
	addq dest,8,dest
	cmplt color,3,scratch
	bne scratch,ZUP_L
	ret     zero,(ra)
TUP_S:
TUP_L:
	lds	ft0,0(src)	; src->h[0].c[i].real 
	lds	ft1,4(src)	; src->h[0].c[i].imag 
	lds	ft2,24(src)	; src->h[1].c[i].real 
	lds	ft3,28(src)	; src->h[1].c[i].imag 

	sts	ft0,0(dest)
	sts	ft1,4(dest)
	sts	ft2,24(dest)
	sts	ft3,28(dest)

	sts	ft0,48(dest)	;  d[0] * +1 
	sts	ft1,52(dest)
	sts	ft2,72(dest)	;  d[1] * +1 
	sts	ft3,76(dest)

	addq color,1,color
	addq src,8,src		; next color 
	addq dest,8,dest
	cmplt color,3,scratch
	bne scratch,TUP_L
	ret     zero,(ra)
XDOWN_S:
XDOWN_L:
	lds	ft0,0(src)	; src->h[0].c[i].real 
	lds	ft1,4(src)	; src->h[0].c[i].imag 
	lds	ft2,24(src)	; src->h[1].c[i].real 
	lds	ft3,28(src)	; src->h[1].c[i].imag 

	cpysn	ft1,ft1,ft6
	cpysn	ft3,ft3,ft5
	sts	ft0,0(dest)
	sts	ft1,4(dest)
	sts	ft2,24(dest)
	sts	ft3,28(dest)

	sts	ft5,48(dest)	;  d[1] * +i 
	sts	ft2,52(dest)
	sts	ft6,72(dest)	;  d[0] * +i 
	sts	ft0,76(dest)

	addq color,1,color
	addq src,8,src		; next color 
	addq dest,8,dest
	cmplt color,3,scratch
	bne scratch,XDOWN_L
	ret     zero,(ra)
YDOWN_S:
YDOWN_L:
	lds	ft0,0(src)	; src->h[0].c[i].real 
	lds	ft1,4(src)	; src->h[0].c[i].imag 
	lds	ft2,24(src)	; src->h[1].c[i].real 
	lds	ft3,28(src)	; src->h[1].c[i].imag 

	sts	ft0,0(dest)
	sts	ft1,4(dest)
	cpysn	ft2,ft2,ft5
	cpysn	ft3,ft3,ft6
	sts	ft2,24(dest)
	sts	ft3,28(dest)

	sts	ft5,48(dest)	;  d[1] * -1 
	sts	ft6,52(dest)
	sts	ft0,72(dest)	;  d[0] * +1 
	sts	ft1,76(dest)

	addq color,1,color
	addq src,8,src		; next color 
	addq dest,8,dest
	cmplt color,3,scratch
	bne scratch,YDOWN_L
	ret     zero,(ra)
ZDOWN_S:
ZDOWN_L:
	lds	ft0,0(src)	; src->h[0].c[i].real 
	lds	ft1,4(src)	; src->h[0].c[i].imag 
	lds	ft2,24(src)	; src->h[1].c[i].real 
	lds	ft3,28(src)	; src->h[1].c[i].imag 

	cpysn	ft1,ft1,ft5
	cpysn	ft2,ft2,ft6
	sts	ft0,0(dest)
	sts	ft1,4(dest)
	sts	ft2,24(dest)
	sts	ft3,28(dest)

	sts	ft5,48(dest)	;  d[0] * +i 
	sts	ft0,52(dest)
	sts	ft3,72(dest)	;  d[1] * -i 
	sts	ft6,76(dest)

	addq color,1,color
	addq src,8,src		; next color 
	addq dest,8,dest
	cmplt color,3,scratch
	bne scratch,ZDOWN_L
	ret     zero,(ra)
TDOWN_S:
TDOWN_L:
	lds	ft0,0(src)	; src->h[0].c[i].real 
	lds	ft1,4(src)	; src->h[0].c[i].imag 
	lds	ft2,24(src)	; src->h[1].c[i].real 
	lds	ft3,28(src)	; src->h[1].c[i].imag 

	sts	ft0,0(dest)
	sts	ft1,4(dest)
	sts	ft2,24(dest)
	sts	ft3,28(dest)
	cpysn	ft0,ft0,ft5
	cpysn	ft1,ft1,ft6
	cpysn	ft2,ft2,ft7
	cpysn	ft3,ft3,ft8

	sts	ft5,48(dest)	;  d[0] * -1 
	sts	ft6,52(dest)
	sts	ft7,72(dest)	;  d[1] * -1 
	sts	ft8,76(dest)

	addq color,1,color
	addq src,8,src		; next color 
	addq dest,8,dest
	cmplt color,3,scratch
	bne scratch,TDOWN_L
	ret     zero,(ra)

	.endp
	.end
